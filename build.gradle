plugins {
	id 'java'
	id 'jacoco'
	id 'checkstyle' 
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
}

group = 'com.panasonic.jp'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2022.0.4")
}

dependencies {
	// Develop Scope
	implementation 'org.springframework.boot:spring-boot-starter-web' // framework
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc' // driver
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.0-M2' // server
	implementation 'org.postgresql:postgresql' // database
	compileOnly 'org.projectlombok:lombok' // compile
 	annotationProcessor 'org.projectlombok:lombok' // annotation

	// Test Scope
	testImplementation 'org.springframework.boot:spring-boot-starter-test' // framework
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jdbc' // driver
	testImplementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.0-M2' // server
	testCompileOnly group: 'com.h2database', name: 'h2', version: '2.2.224' // database
  	testCompileOnly 'org.projectlombok:lombok' // compile
  	testAnnotationProcessor 'org.projectlombok:lombok' // annotation

	// misc
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

task buildZip(type: Zip) {
  from compileJava
  from processResources
  into('lib') {
    from(configurations.compileClasspath) {
      exclude 'tomcat-embed-*'
    }
  }
}

build.dependsOn buildZip

// jacoco start
jacoco {
    toolVersion = "0.8.9"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
	reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}
// jacoco end

// checkstyle start
checkstyle {
    toolVersion = '10.12.3'
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
        // html.stylesheet resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}
// checkstyle end
